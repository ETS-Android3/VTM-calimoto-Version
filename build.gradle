buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

allprojects {
    group = 'org.mapsforge'
    version = '1.3.4'
    ext.isReleaseVersion = false // !version.endsWith("SNAPSHOT")

    ext.androidBuildVersionTools = "29.0.1"
    ext.gdxVersion = "1.9.9"
    ext.gwtVersion = "2.8.0"
    ext.slf4jVersion = "1.7.25"

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
            logging.captureStandardError LogLevel.INFO
            logging.captureStandardOutput LogLevel.INFO
        }
    }
}

static def androidCompileSdk() { return 29 }

// 14 for Support Library, 16 for sqlite-android
static def androidMinSdk() { return 16 }

static def androidTargetSdk() { return 22 }

static def versionCode() { return 1 }

def versionName() { return version }

subprojects {
    repositories {
        google()
        maven { url 'https://repo.boundlessgeo.com/main/' }
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://jitpack.io' }
    }

    // see https://github.com/calimoto-GmbH/calimoto-logic
    ext.depPaths = [
            'caloPublic': '../../dependencies/release/calimoto-logic-public/core-1.2.jar',
    ]

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(Jar) {
        if (project.name.equals("vtm")
                || project.name.equals("vtm-themes")
                || project.name.equals("vtm-android")
                || project.name.equals("vtm-http")
                || project.name.equals("vtm-mvt")) {
            destinationDir = file("$rootDir/../dependencies/debug/VTM-mvt")
        }
    }
}
